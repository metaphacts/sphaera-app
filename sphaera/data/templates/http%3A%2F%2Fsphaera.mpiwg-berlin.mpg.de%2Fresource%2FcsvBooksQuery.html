SELECT
	?subject
	?custom_identifier
	?label
	?format
	?date_text
	?year_published_from
	?year_published_to
	?place
	?book_type
	(SAMPLE(?all_coordinates) as ?coordinates)
	?fingerprint
	(GROUP_CONCAT(DISTINCT ?language; SEPARATOR=';') as ?languages)
	(GROUP_CONCAT(DISTINCT ?author; SEPARATOR=';') as ?authors)
	(GROUP_CONCAT(DISTINCT ?publisher; SEPARATOR=';') as ?publishers)
	(GROUP_CONCAT(DISTINCT ?printer; SEPARATOR=';') as ?printers)
WHERE {
	?subject a sphaera:Book ;
		ecrm:P3_has_note ?label .
	OPTIONAL {
		?subject ecrm:P43_has_dimension/ecrm:P3_has_note ?format .
	}
	OPTIONAL {
		?subject ecrm:P128_carries/efrbroo:R24i_was_created_through/ecrm:P4_has_time-span ?date_published .
		OPTIONAL {
			?date_published ecrm:P3_has_note ?date_text .
		}
		OPTIONAL {
			?date_published ecrm:P82a_begin_of_the_beginning ?date_published_from .
			BIND(SUBSTR(STR(?date_published_from),0,4) as ?year_published_from)
		}
		OPTIONAL {
			?date_published ecrm:P82b_end_of_the_end ?date_published_to .
			BIND(SUBSTR(STR(?date_published_to),0,4) as ?year_published_to)
		}
	}
	OPTIONAL {
		?subject ecrm:P128_carries/ecrm:P72_has_language/ecrm:P3_has_note ?language
	}
	OPTIONAL {
		?subject ecrm:P128_carries/efrbroo:R24i_was_created_through/ecrm:PC01_is_domain_of/ecrm:PC02_has_range/ecrm:P48_has_preferred_identifier/ecrm:P3_has_note ?publisher .
	}
	OPTIONAL {
		?subject efrbroo:R28i_was_produced_by/ecrm:PC01_is_domain_of/ecrm:PC02_has_range/ecrm:P48_has_preferred_identifier/ecrm:P3_has_note ?printer .
	}
	OPTIONAL {
		?subject ecrm:P128_carries/ecrm:P165_incorporates/efrbroo:R17i_was_created_by/ecrm:PC01_is_domain_of/ecrm:PC02_has_range/ecrm:P48_has_preferred_identifier/ecrm:P3_has_note ?author .
	}
	OPTIONAL {
		?subject ecrm:P128_carries/efrbroo:R24i_was_created_through/ecrm:P7_took_place_at ?placeUri .
		?placeUri ecrm:P3_has_note ?place
		OPTIONAL {
			?placeUri owl:sameAs ?wdPlace .
			SERVICE <https://query.wikidata.org/sparql> {
				?wdPlace wdt:P625 ?coord .
			}
			BIND( CONCAT(
				STRBEFORE(STRAFTER(STR(?coord), ' '), ')'),
    		', ',
        STRBEFORE(STRAFTER(STR(?coord), 'Point('), ' ')
			) as ?all_coordinates)
		}
	}
  OPTIONAL {
      ?subject ecrm:P1_is_identified_by ?fingerprintEntity .
      ?fingerprintEntity ecrm:P3_has_note ?fingerprint ;
          ecrm:P2_has_type sphaera:fingerprint .
  }
	OPTIONAL {
		?subject ecrm:P1_is_identified_by ?identifier .
		?identifier ecrm:P2_has_type sphaera:customIdentifier ;
			ecrm:P3_has_note ?custom_identifier .
	}
	OPTIONAL {
		?subject ecrm:P128_carries/ecrm:P165_incorporates/ecrm:P72_has_language/ecrm:P3_has_note ?language .
	}
  OPTIONAL {
  	?subject ecrm:P2_has_type/ecrm:P3_has_note ?book_type .
  }
}
GROUP BY ?subject ?type ?format ?date_text ?year_published_from ?year_published_to ?label ?book_type ?place ?custom_identifier ?fingerprint ?coordinates
ORDER BY ?year_published_from ?date_text
